#!/bin/zsh

##? zr - reload zsh config. Inside tmux, reloads all zsh panes
##?      across every session/window. Outside tmux, reloads the
##?      current shell.
##?
##? usage:
##?   zr [-q | --quiet]
##?
##? options:
##?   -q, --quiet  suppress output

zr() {
  [[ "$1" == "-h" || "$1" == "--help" ]] && { grep "^##?" "${(%):-%x}" | cut -c 5-; return 0; }

  local quiet=false
  [[ "$1" == "-q" || "$1" == "--quiet" ]] && quiet=true

  if [[ -n "$TMUX" ]]; then
    local pane cmd count=0
    tmux list-panes -a -F '#{session_name}:#{window_index}.#{pane_index} #{pane_current_command}' \
      | while read pane cmd; do
          if [[ $cmd == zsh ]]; then
            tmux send-keys -t "$pane" "exec zsh" Enter
            (( count++ ))
            $quiet || echo "  ðŸ”„ $pane"
          fi
        done
    $quiet || echo "âœ… Reloaded $count zsh pane(s)"
  else
    $quiet || echo "ðŸ”„ Reloading shell..."
    exec zsh
  fi
}

# vim: ft=zsh
