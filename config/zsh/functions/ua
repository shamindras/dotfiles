#!/bin/zsh

##? ua - activate the nearest uv python virtual environment
##?
##? usage:
##?   ua
##?
##? behavior:
##?   traverses up the directory tree from the current directory searching for
##?   a valid uv python virtual environment (uv.lock + .venv/bin/activate).
##?   if found, activates the virtual environment. Always returns to the
##?   starting directory regardless of outcome.
##?
##? examples:
##?   ua                                     # activate nearest uv environment
##?   cd ~/projects/myproject/src && ua      # finds ~/projects/myproject/.venv

ua() {
  [[ "$1" == "-h" || "$1" == "--help" ]] && { grep "^##?" "${(%):-%x}" | cut -c 5-; return 0; }

  local initial_dir=$PWD
  local current_dir=$PWD
  local found_env=false
  local env_dir=""

  while [[ $current_dir != "/" ]]; do
    if [[ -f "$current_dir/uv.lock" ]] && [[ -d "$current_dir/.venv" ]]; then
      if [[ -f "$current_dir/.venv/bin/activate" ]]; then
        print "Found uv environment in: $current_dir"
        env_dir=$current_dir
        found_env=true
        break
      else
        print "Warning: Found incomplete uv environment in $current_dir (.venv/bin/activate missing)"
      fi
    fi

    cd ..
    current_dir=$PWD
  done

  # Return to initial directory regardless of success
  cd "$initial_dir" || return

  # If environment was found, activate it from the initial directory
  if $found_env; then
    source "$env_dir/.venv/bin/activate"
    return 0
  else
    print "Error: No valid uv environment found in parent directories"
    return 1
  fi
}

# vim: ft=zsh
